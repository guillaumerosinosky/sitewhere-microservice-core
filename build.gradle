plugins {
    id 'org.springframework.boot' version '2.4.5' apply false
	id 'com.github.hierynomus.license' version '0.15.0' 
	id 'org.ajoberstar.grgit' version '2.2.1' 
    id 'java'
    id 'distribution'
}

ext {
	git = org.ajoberstar.grgit.Grgit.open(currentDir: file('.'))
}

ext['git.revision'] = ext.git.head().id
ext['git.revision.abbrev'] = ext.git.head().abbreviatedId

ext['sitewhere.k8s.api.version'] = '3.1.0'
ext['sitewhere.grpc.api.version'] = '3.1.0'

apply plugin: 'distribution'
	
allprojects {
	group = 'com.sitewhere'
	version = '3.1.0'
	
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url 'https://repo.spring.io/release' }
    }
}

subprojects {
	apply plugin: "com.github.hierynomus.license"
	license {
		header rootProject.file('HEADER')
		include "**/*.java"
	}

	// Choose Java settings.
	apply plugin: 'java-library'
	sourceCompatibility = 1.8
	targetCompatibility = 1.8
	
	// Common dependencies used for all projects.
	dependencies {
        // Sping dependency managment.
        implementation platform(org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES)
        
        // Spring Boot dependencies.
        api('org.springframework.boot:spring-boot-autoconfigure') {
            exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
        }
        api 'org.springframework.experimental:spring-native:0.9.2'
        api 'org.springframework.security:spring-security-web'
        implementation 'ch.qos.logback:logback-classic'
        implementation 'org.slf4j:jul-to-slf4j'

        // Native image dependencies.
		compileOnly group: 'org.graalvm.nativeimage', name: 'svm', version: '21.0.0.2'
		
		testCompile group: 'junit', name: 'junit', version: '4.12'
	}
}
